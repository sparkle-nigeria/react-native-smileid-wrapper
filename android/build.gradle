buildscript {
  ext.getExtOrDefault = {name ->
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['RnWrap_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
    classpath "org.jetbrains.kotlin:compose-compiler-gradle-plugin:$kotlinVersion"
    classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
  }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"

apply plugin: "com.facebook.react"
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["RnWrap_" + name]).toInteger()
}

android {
  namespace "com.rnwrap"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_17.toString()
    freeCompilerArgs += ['-Xskip-metadata-version-check']
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  buildFeatures {
    compose true
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni"
      ]
    }
  }
}

repositories {
  mavenCentral()
  google()
}

/**
 * The Smile Android sdk will always use a newer kotlin version, but you can override it to use the
 * version that is used by the React Native version we are using to ensure maximum compatibility ):
 */
allprojects {
  configurations.configureEach {
    resolutionStrategy {
      force(
        "org.jetbrains.kotlin:kotlin-stdlib:2.0.21",
        "org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.0.21",
        "org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.0.21",
        "org.jetbrains.kotlin:kotlin-stdlib-common:2.0.21",
        "org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.0",
        "org.jetbrains.kotlinx:kotlinx-serialization-core:1.7.0"
      )

      eachDependency { details ->
        if (details.requested.group == "org.jetbrains.kotlin") {
          details.useVersion("2.0.21")
          details.because("Force Kotlin 2.0.21 to override SmileID SDK's Kotlin 2.2.0")
        }
        if (details.requested.group == "org.jetbrains.kotlinx" &&
          details.requested.name.startsWith("kotlinx-serialization")) {
          details.useVersion("1.7.0")
          details.because("Force kotlinx-serialization 1.7.0 for compatibility with Kotlin 2.0.21")
        }
      }
    }
  }
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android"
    // SmileID Android SDK
      implementation("com.smileidentity:android-sdk:11.1.4") {
          exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
          exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
          exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
          exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-common'
          exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-serialization-core'
          exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-serialization-json'
          exclude group: "androidx.activity", module: "activity-ktx"
          exclude group: "androidx.core", module: "core"
          exclude group: "androidx.core", module: "core-ktx"
          exclude group: "androidx.activity", module: "activity-compose"
          exclude group: "com.squareup.okhttp3"
      }
      implementation("org.jetbrains.kotlin:kotlin-stdlib:2.0.21")
      implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.0.21")
      implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.7.0")
      implementation 'androidx.fragment:fragment-ktx:1.8.8'
      implementation("com.squareup.okhttp3:okhttp:4.12.0")
      implementation("com.squareup.okhttp3:logging-interceptor:4.12.0")
      implementation("androidx.compose.runtime:runtime:1.7.5")
      implementation("androidx.activity:activity-ktx:1.10.1")
      implementation("androidx.core:core:1.16.0")
      implementation("androidx.core:core-ktx:1.16.0")
      implementation("androidx.activity:activity-compose:1.10.1")

}

react {
  jsRootDir = file("../src/")
  libraryName = "RnWrapView"
  codegenJavaPackageName = "com.rnwrap"
}
